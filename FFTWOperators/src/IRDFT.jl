export IRDFT

"""
	IRDFT([domain_type=Float64::Type,] dim_in::Tuple, d::Int, [,dims=1])
	IRDFT(x::AbstractArray, d::Int, [,dims=1])

Creates a `LinearOperator` which, when multiplied with a complex array `x`, returns the IDFT over the dimension `dims`, exploiting Hermitian symmetry. Like in the function `BASE.irfft`, `d` must satisfy `div(d,2)+1 == size(x,dims)`.

```jldoctest
julia> using FFTWOperators

julia> A = IRDFT(Complex{Float64},(10,),19)
ℱ⁻¹  ℂ^10 -> ℝ^19

julia> A = IRDFT((5,10,8),19,2)
ℱ⁻¹  ℂ^(5, 10, 8) -> ℝ^(5, 19, 8)
	
```
"""
struct IRDFT{T <: Number, N, D, T1 <: AbstractFFTs.Plan, T2 <: AbstractFFTs.Plan, T3 <: NTuple{N, Any}, S} <:
    LinearOperator
    dim_in::NTuple{N, Int}
    dim_out::NTuple{N, Int}
    A::T1
    At::T2
    idx::T3
    # Plan-time FFTW thread count; c2r is the inverse of the r2c measured above.
    num_threads::Int
end

# Constructors
#standard constructor

function IRDFT(
        x::AbstractArray{Complex{T}, N}, d::Int, dims::Int = 1;
        num_threads = nothing, threaded::Bool = true
    ) where {T <: Number, N}
    nthr = _fftw_num_threads(:r2c, num_threads, threaded, length(x))
    A = _with_fftw_threads(() -> plan_irfft(x, d, dims), nthr)
    dim_in = size(x)
    dim_out = ()
    idx = ()
    for i in 1:N
        dim_out = i == dims ? (dim_out..., d) : (dim_out..., dim_in[i])
        idx = i == dims ? (idx..., 2:ceil(Int, d / 2)) : (idx..., Colon())
    end
    At = _with_fftw_threads(() -> plan_rfft(similar(x, T, dim_out), dims), nthr)
    S = _array_wrapper_type(typeof(x isa SubArray ? parent(x) : x))
    return IRDFT{T, N, dims, typeof(A), typeof(At), typeof(idx), S}(
        dim_in, dim_out, A, At, idx, nthr
    )
end

function IRDFT(T::Type, dim_in::NTuple{N, Int}, d::Int, dims::Int = 1; kwargs...) where {N}
    return IRDFT(zeros(T, dim_in), d, dims; kwargs...)
end
function IRDFT(dim_in::NTuple{N, Int}, d::Int, dims::Int = 1; kwargs...) where {N}
    return IRDFT(zeros(Complex{Float64}, dim_in), d, dims; kwargs...)
end

# Mappings

function mul!(
        y::C1, L::IRDFT{T, N, D, T1, T2, T3}, b::C2
    ) where {N, T, D, T1, T2, T3, C1 <: AbstractArray{T, N}, C2 <: AbstractArray{Complex{T}, N}}
    check(y, L, b)
    mul!(y, L.A, b)
    return y
end

function mul!(
        y::C2, L::AdjointOperator{<:IRDFT{T, N, D}}, b::C1
    ) where {N, T, D, C1 <: AbstractArray{T, N}, C2 <: AbstractArray{Complex{T}, N}}
    check(y, L, b)
    A = L.A
    mul!(y, A.At, b)
    y ./= size(b, D)
    @views y[A.idx...] .*= 2
    return y
end

# Properties

size(L::IRDFT) = (L.dim_out, L.dim_in)

fun_name(A::IRDFT) = "ℱ⁻¹"

domain_type(::IRDFT{T}) where {T} = Complex{T}
codomain_type(::IRDFT{T}) where {T} = T
is_thread_safe(::IRDFT) = true

function domain_array_type(::IRDFT{T, N, D, T1, T2, T3, S}) where {T, N, D, T1, T2, T3, S}
    return S{Complex{T}}
end
function codomain_array_type(::IRDFT{T, N, D, T1, T2, T3, S}) where {T, N, D, T1, T2, T3, S}
    return S{T}
end

is_AAc_diagonal(L::IRDFT) = false #TODO but might be true?
is_invertible(L::IRDFT) = true
is_full_row_rank(L::IRDFT) = true

has_fast_opnorm(::IRDFT) = true
LinearAlgebra.opnorm(L::IRDFT{T}) where {T} = sqrt(prod(L.dim_out)::Int / 2)

# ─── Threading ────────────────────────────────────────────────────────────────

is_threaded(op::IRDFT) = op.num_threads > 1
supports_threading(::IRDFT) = true

function _copy_operator_impl(
        op::IRDFT{T, N, D, T1, T2, T3, S}; storage_type = nothing, threaded = nothing
    ) where {T, N, D, T1, T2, T3, S}
    new_threaded = threaded === nothing ? is_threaded(op) : threaded
    # No per-call scratch fields, so an unchanged storage type and thread count means the
    # plans can be shared.
    if storage_type === nothing && new_threaded == is_threaded(op)
        return IRDFT{T, N, D, T1, T2, T3, S}(
            op.dim_in, op.dim_out, op.A, op.At, op.idx, op.num_threads
        )
    end
    # No persistent data to carry over (IRDFT holds only plans), so the prototype can be
    # uninitialized.
    new_storage = storage_type === nothing ? S : storage_type
    return IRDFT(similar(new_storage{Complex{T}}, op.dim_in), op.dim_out[D], D; threaded = new_threaded)
end
